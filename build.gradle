buildscript {
    ext {
        restdocsApiSpecVersion = '0.18.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'com.epages.restdocs-api-spec' version "$restdocsApiSpecVersion"
}

group = 'com.megazone'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
    springCloudVersion = '2022.0.3'
    mybatisVersion = '3.0.2'
}

openapi3 {
    server = 'http://localhost:8080'
    title = 'REST API 문서'
    description = 'REST API 문서'
    version = '0.0.1'
    format = 'yaml'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    runtimeOnly 'com.h2database:h2'
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:$mybatisVersion"
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // tracer dependency
    implementation 'io.micrometer:micrometer-tracing-bridge-brave' // tracer
    implementation 'io.micrometer:micrometer-registry-prometheus' // for prometheus metrics
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:$mybatisVersion"
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:$restdocsApiSpecVersion"
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
            include "**/*.xml"
        }
    }
}

tasks.register('copyResourcesMain', Copy) {
    from(sourceSets.main.resources.srcDirs) {
        exclude("**/*.java")
    }
    into("build/classes/java/main")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.compileJava.dependsOn(copyResourcesMain)

bootJar {
    archiveFileName = 'application.jar'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

tasks.register('copyOasToSwagger', Copy) {
    dependsOn("openapi3")

    from("$buildDir/api-spec/openapi3.yaml")
    into("src/main/resources/static/docs/swagger")
}

tasks.register('copyDocument', Copy) {
    dependsOn asciidoctor

    from("$buildDir/docs/asciidoc")
    into("src/main/resources/static/docs/restdocs")
}
