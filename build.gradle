buildscript {
    ext {
        restdocsApiSpecVersion = '0.18.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'com.epages.restdocs-api-spec' version "$restdocsApiSpecVersion"
}

group = 'com.megazone'
version = '0.0.1-SNAPSHOT'
apply from: 'gradle-scripts/docs.gradle'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = '2022.0.4'
    mybatisVersion = '3.0.2'
    shedlockVersion = '5.6.0'
    mapStructVersion = '1.5.5.Final'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    runtimeOnly 'com.h2database:h2'
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:$mybatisVersion"
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // tracer dependency
    implementation 'io.micrometer:micrometer-tracing-bridge-brave' // tracer
    implementation 'io.micrometer:micrometer-registry-prometheus' // for prometheus metrics

    implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation "net.javacrumbs.shedlock:shedlock-spring:$shedlockVersion"
    implementation "net.javacrumbs.shedlock:shedlock-provider-jdbc-template:$shedlockVersion"

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation "org.mapstruct:mapstruct:$mapStructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:$mybatisVersion"
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:$restdocsApiSpecVersion"
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
            include "**/*.xml"
        }
    }
}

tasks.register('copyResourcesMain', Copy) {
    from(sourceSets.main.resources.srcDirs) {
        exclude("**/*.java")
    }
    into("build/classes/java/main")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.compileJava.dependsOn(copyResourcesMain)

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

bootJar {
    archiveFileName = 'application.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
